<?xml version="1.0" encoding="utf-8"?>
<!--
/*******************************************************************************
 * PushButton Engine
 * Copyright (C) 2009 PushButton Labs, LLC
 * For more information see http://www.pushbuttonengine.com
 * 
 * This file is property of PushButton Labs, LLC and NOT under the MIT license.
 ******************************************************************************/
-->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:local="*" xmlns:ui="com.pblabs.rendering2D.ui.*" 
   layout="absolute" frameRate="60" width="640" height="480" applicationComplete="_OnLoaded()"
   activate="application1_activateHandler(event)" deactivate="application1_deactivateHandler(event)"
   preloader="CustomPreloader"
   backgroundColor="#FFFFFF" backgroundGradientColors="[0xFFFFFF,0xFFFFFF]">
   <mx:Style source="Style.css"/>
   
   <ui:FlexSceneView name="MainView" bottom="0" top="0" left="0" right="0"/>
   <mx:Label x="470" y="13" text="Score: 999999" id="lblScore"/>
   <mx:Label x="480" y="40" text="Time: 999999" id="lblTime"/>
   
   <local:Levels/>
   <local:Components/>
   <mx:Script>
      <![CDATA[
         import com.pblabs.engine.core.*;
         import com.pblabs.RollyGame.*;
         import mx.controls.Alert;
         
         public var CurrentScore:Number = 0;
         public var StartTimer:Number = 0;
         public var CurrentTime:Number = 60.0;
         public var LevelDuration:Number = 45000;
         
         public var Resources:GameResources = new GameResources();
          
         public function AddPoints(amount:Number):void
         {
            CurrentScore += amount;
            lblScore.text = "Score: " + CurrentScore;
         }
         
         private function _OnLoaded():void
         {
            // Initialize level and score.
            Global.startup(this);
            AddPoints(0);
            
            // Set up the game timer.
            StartTimer = ProcessManager.instance.virtualTime;
            setInterval(_UpdateTimer, 10);
            
            // Make the game scale properly.
            Application.application.stage.scaleMode = StageScaleMode.SHOW_ALL; 
            
            // Load level zero.
            LevelManager.instance.start(0);
         }
         
         private function _UpdateTimer():void
         {   
            stage.focus = stage;

            CurrentTime = LevelDuration - (ProcessManager.instance.virtualTime - StartTimer);
            
            if(CurrentTime >= 0)
               lblTime.text = "Time: " + (CurrentTime/1000).toFixed(2);
            else
               lblTime.text = "Time: 0.00";
            
            if(CurrentTime <= 0 && ProcessManager.instance.isTicking)
            {
               // Stop playing!
               ProcessManager.instance.stop();
               
               // Kick off the scoreboard.
               var sb:Scoreboard = new Scoreboard();
               addChild(sb);
               sb.StartReport(CurrentScore);
            }
         }
         
         public function ResetLevel():void
         {
            // Reset the level.
            LevelManager.instance.unloadCurrentLevel();
            LevelManager.instance.loadLevel(LevelManager.instance.currentLevel);
            
            // Reset the timer and score.
            StartTimer = ProcessManager.instance.virtualTime;
            AddPoints(-CurrentScore);
         }
         
         public function RestartGame():void
         {
            // Reset the level.
            LevelManager.instance.unloadCurrentLevel();
            LevelManager.instance.loadLevel(0);
            
            // Reset the timer and score.
            StartTimer = ProcessManager.instance.virtualTime;
            AddPoints(-CurrentScore);
         }
         
         public function NextLevel():void
         {
            if(LevelManager.instance.currentLevel < 1)
            {
               LevelManager.instance.unloadCurrentLevel();
               LevelManager.instance.loadNextLevel();               

               // Reset the timer.
               StartTimer = ProcessManager.instance.virtualTime;
	           ProcessManager.instance.start();
               AddPoints(-CurrentScore);
            }
            else
            {
            	addChild(new GameOver());
            }
         }

         protected function application1_activateHandler(event:Event):void
         {
            ProcessManager.instance.start();
         }

         protected function application1_deactivateHandler(event:Event):void
         {
            ProcessManager.instance.stop();
         }

      ]]>
   </mx:Script>
</mx:Application>